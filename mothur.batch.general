# Adjust to the number of processors available
set.current(processors=8)
# Replace tempdefault path by path to database directory
set.dir(tempdefault=/nfs/vdenef-lab/Shared/Ruben/databases_taxass)
pcr.seqs(fasta=silva.seed_v123.align, start=11894, end=25319, keepdots=F)
make.contigs(file=stability.file)
screen.seqs(fasta=current, group=current, maxambig=0, minlength=240, maxhomop=8, maxlength=275)
summary.seqs(fasta=current)
unique.seqs()
count.seqs(name=current, group=current)
align.seqs(seed=777,fasta=current, reference=silva.seed_v123.pcr.align,flip=T)
screen.seqs(fasta=current, count=current, start=1968, end=11550)
summary.seqs(count=current)
filter.seqs(fasta=current, vertical=T, trump=.)
unique.seqs(fasta=current, count=current)
pre.cluster(fasta=current, count=current, diffs=2)
chimera.uchime(fasta=current, count=current, dereplicate=t)
remove.seqs(fasta=current, accnos=current)
summary.seqs(count=current)
# Replace path by path to database directory - this will remove all possible 8mer, train and sum files prior to classifying.
system(rm /nfs/vdenef-lab/Shared/Ruben/databases_taxass/*8mer* /nfs/vdenef-lab/Shared/Ruben/databases_taxass/*train* /nfs/vdenef-lab/Shared/Ruben/databases_taxass/*sum*)
classify.seqs(seed=777, fasta=current, template=silva.nr_v123.align, taxonomy=silva.nr_v123.tax, method=wang, probs=T, cutoff=80)
remove.lineage(fasta=current, count=current, taxonomy=current, taxon=Mitochondria-unknown-Archaea-Eukaryota-Chloroplast)
cluster.split(fasta=current, count=current, taxonomy=current, splitmethod=classify, taxlevel=4, cutoff=0.15, method=average)
summary.seqs(count=current)
make.shared(list=current, count=current, label=0.03)
classify.otu(list=current, count=current, taxonomy=current, label=0.03)
get.oturep(list=current, method=abundance, fasta=current, name=current, label=0.03)
deunique.seqs(fasta=stability.trim.contigs.good.unique.good.filter.unique.fasta, name=stability.trim.contigs.good.names)
system(cat stability.contigs.good.groups| sed 's/_/:/g' > intermediate1)
system(paste stability.contigs.good.groups intermediate1 | awk 'BEGIN{FS="\t"}{print $1"\t"$2"_"$3}' > intermediate2)
# Replace path by path to renamer.pl script
system(perl /nfs/vdenef-lab/Shared/Ruben/scripts_taxass/renamer.pl stability.trim.contigs.good.unique.good.filter.unique.redundant.fasta intermediate2)
system(o-pad-with-gaps *_headers-replaced.fasta)
system(decompose *-PADDED-WITH-GAPS)
system(mv ./stability-m0.10-A0-M0-d4/NODE-REPRESENTATIVES.fasta ./stability-m0.10-A0-M0-d4/NODE_REPRESENTATIVES.fasta)
# Replace path by path to database directory - this will remove all possible 8mer, train and sum files prior to classifying.
system(rm /nfs/vdenef-lab/Shared/Ruben/databases_taxass/*8mer* /nfs/vdenef-lab/Shared/Ruben/databases_taxass/*train* /nfs/vdenef-lab/Shared/Ruben/databases_taxass/*sum*)
classify.seqs(seed=777, fasta=NODE_REPRESENTATIVES.fasta, template=silva.nr_v123.align, taxonomy=silva.nr_v123.tax, method=wang, probs=T, cutoff=80, inputdir=./stability-m0.10-A0-M0-d4, outputdir=./stability-m0.10-A0-M0-d4)
# Replace path by path to make_phyloseq_general.R script
system(Rscript /nfs/vdenef-lab/Shared/Ruben/scripts_taxass/make_phyloseq_general.R stability.trim.contigs.good.unique.good.filter.unique.precluster.pick.pick.an.unique_list.shared stability.trim.contigs.good.unique.good.filter.unique.precluster.pick.pick.an.unique_list.0.03.cons.taxonomy ./stability-m0.10-A0-M0-d4/MATRIX-COUNT.txt ./stability-m0.10-A0-M0-d4/NODE_REPRESENTATIVES.nr_v123.wang.taxonomy)
